apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.java.srcDirs
    from sourceSets.main.resources.srcDirs
}

task javadocJar(type: Jar) {
    archiveClassifier.set('javadoc')
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                from components.java

                artifact sourcesJar
                artifact javadocJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'FreshBooks SDK'
                    url = 'https://github.com/amcintosh/freshbooks-java-sdk'
                    licenses {
                        license {
                            name = 'FreshBooks SDK License'
                            url = 'https://github.com/amcintosh/freshbooks-java-sdk/blob/master/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'amcintosh'
                            name = 'Andrew McIntosh'
                            email = 'andrew@amcintosh.net'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/amcintosh/freshbooks-java-sdk.git'
                        developerConnection = 'scm:git:ssh://github.com/amcintosh/freshbooks-java-sdk.git'
                        url = 'https://github.com/amcintosh/freshbooks-java-sdk/tree/master'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}
